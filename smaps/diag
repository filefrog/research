#!/usr/bin/perl

sub parse
{
	my ($file) = @_;
	my @smaps;
	my $this;

	open my $fh, "<", $file or return;
	while (<$fh>) {
		if (m/^([0-9a-f]+)-([0-9a-f]+) (\S+) ([0-9a-f]+) ([0-9a-f]+:[0-9a-f]+) ([0-9]+)\s+(\S*)$/) {
			$this = {
				address => { start => $1, end => $2 },
				mode    => $3,
				offset  => int($4),
				dev     => $5,
				inode   => int($6),
				path    => $7 || '[mmap]',
			};
			push @smaps, $this;
			next;
		}
		next unless $this;

		   if (m/^(\S+):\s+([0-9]+) kB$/) { $this->{usage}{lc $1} = $2 * 1024; }
		elsif (m/&(\S+):\s+(.*)$/)        { $this->{usage}{lc $1} = $2; }
	}
	close $fh;

	return @smaps;
}
sub summarize
{
	my %sum;
	for my $m (@_) {
		$sum{$m->{path}}{usage}{$_} += $m->{usage}{$_}
			for keys %{ $m->{usage} };
	}
	return \%sum;
}
sub dumbdown
{
	my %sum;
	for my $m (@_) {
		my $path = $m->{path};
		$path = '[libs]'  if $path =~ m{/lib/|\.so};
		$path = '[stack]' if $path =~ m{^\[stack};
		$path = '[mmap]'  if $path =~ m{^/};
		$sum{$path}{usage}{$_} += $m->{usage}{$_}
			for keys %{ $m->{usage} };
	}
	return \%sum;
}

sub num
{
	my ($n) = @_;
	return sprintf("%8s  ", "-") if !$n;
	return sprintf("%8.1lf k", $n) if $n < 1024;
	$n /= 1024;
	return sprintf("%8.1lf M", $n) if $n < 1024;
	$n /= 1024;
	return sprintf("%8.1lf G", $n);
}

my $pid = $ARGV[0] or die "USAGE: $0 PID\n";
my $x = dumbdown(parse "/proc/$pid/smaps");
for my $m (sort keys %$x) {
	next if $m =~ m/^\[(vdso|vsys|libs)/;
	print "$m:\n";

	printf "  private %s [clean]  %s [dirty]\n", map { num($x->{$m}{usage}{"private_$_"} / 1024) } qw/clean dirty/;
	printf "   shared %s [clean]  %s [dirty]\n", map { num($x->{$m}{usage}{"shared_$_"}  / 1024) } qw/clean dirty/;
	print "\n";
}
